plugins {
	id 'org.springframework.boot' version '2.7.8'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'java'
    id 'jacoco'
	id "org.ysb33r.doxygen" version "0.7.0"
	id "org.sonarqube" version "3.5.0.2730"
	id "com.diffplug.spotless" version "6.12.0"
	id "com.github.node-gradle.node" version "3.5.0"
}

processResources {
	dependsOn 'copyFrontendToBuild'
}

task copyFrontendToBuild(type: Copy) {
	dependsOn 'npm_run_build'
	from 'frontend/dist'
	into 'src/main/resources/public'
}

jar {
	dependsOn copyFrontendToBuild
    enabled = false
}

group = 'de.hsrm.mi.team3'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = 17
targetCompatibility = 17

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
	implementation 'org.python:jython-slim:2.7.3'
	implementation 'commons-io:commons-io:2.6'
	implementation 'org.json:json:20220924'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
	toolVersion = "0.8.8"
}

jacocoTestReport {
	dependsOn test
	reports {
	xml.required = true
	}
}

doxygen {
	source new File(projectDir,'src/main/java')
	// Optionen vgl Doxyfile-Dokumentation, aber in Kleinschrift
	// https://www.stack.nl/~dimitri/doxygen/manual/config.html
	generate_html true
	generate_latex false
	javadoc_autobrief true
	optimize_output_java true
	tab_size 4
	brief_member_desc true
	markdown_support true
	autolink_support true
	extract_all true
	inline_info true
	inline_sources true
	source_tooltips true
	class_diagrams true
	have_dot true
	generate_treeview true
}

spotless {
  java {
	target 'src/*/java/**/*.java'
    removeUnusedImports()
    googleJavaFormat()   // has its own section below
    formatAnnotations()  // fixes formatting of type annotations, see below
  }
}

node {
    // Version of node to use.
    version = '18.12.1'

    // Version of npm to use.
    npmVersion = '8.19.2'

	nodeProjectDir = file("${project.projectDir}/frontend")
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

tasks.named("bootJar"){
	requiresUnpack '**/jython*.jar'
}
